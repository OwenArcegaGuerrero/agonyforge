plugins {
    id 'info.solidsoft.pitest' version '1.9.0'
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.palantir.docker' version '0.34.0'
    id 'java'
}

group = 'com.agonyforge'
version = '0.0.2'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

configurations.maybeCreate("pitest")

dependencies {
    pitest 'org.pitest:pitest-junit5-plugin:1.1.0'

    implementation 'com.hazelcast:hazelcast:5.2.0'
    implementation 'io.projectreactor.netty:reactor-netty:1.0.24'

    implementation 'org.webjars:webjars-locator:0.46'
    implementation 'org.webjars:bootstrap:5.2.2'
    implementation 'org.webjars:jquery:3.6.1'
    implementation 'org.webjars.npm:webstomp-client:1.2.6'
    implementation 'org.webjars.npm:sockjs-client:1.6.1'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.session:spring-session-hazelcast'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false // don't build the non-repackaged jar
}

bootJar {
    manifest {
        attributes(
            'Implementation-Title': jar.getArchiveBaseName().get(),
            'Implementation-Version': getArchiveVersion()
        )
    }
}

pitest {
    coverageThreshold = 70
    mutationThreshold = 70
    timestampedReports = false

    excludedMethods = [
        "hashCode",
        "equals"
    ]

    excludedClasses = [
        "com.agonyforge.mud.config.*"
    ]
}

docker {
    name "scionaltera/mud"
    dockerfile file('src/main/docker/local/Dockerfile')
    files bootJar.outputs
}

build.dependsOn tasks.docker
tasks.pitest.mustRunAfter test
check.dependsOn tasks.pitest
